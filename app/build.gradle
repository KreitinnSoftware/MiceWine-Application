plugins {
    id 'com.android.application'
}

android {
    namespace 'com.micewine.emu'
    //noinspection GradleDependency
    compileSdk 35
    ndkVersion "26.1.10909125"

    defaultConfig {
        applicationId "com.micewine.emu"
        minSdk 28
        //noinspection ExpiredTargetSdkVersion
        targetSdk 28
        versionCode 11
        versionName "v0.4.0"
        buildConfigField "String", "GIT_SHORT_SHA", "\"" + ("git rev-parse --short HEAD\n".execute().getText().trim() ?: (System.getenv('CURRENT_COMMIT') ?: "NO_COMMIT")) + "\""
    }

    splits.abi {
        enable true
        reset()

        include "arm64-v8a", "x86_64"
        universalApk true
    }

    compileOptions.sourceCompatibility JavaVersion.VERSION_17
    compileOptions.targetCompatibility JavaVersion.VERSION_17
    packagingOptions.jniLibs.useLegacyPackaging true

    buildTypes.debug {
        signingConfig signingConfigs.debug
        postprocessing {
            removeUnusedCode true
            removeUnusedResources false
            obfuscate false
            optimizeCode true
            minifyEnabled false
        }
    }

    sourceSets {
        main {
            aidl.srcDirs += ['src/main/aidl']
        }
    }

    buildFeatures {
        aidl true
        buildConfig true
        viewBinding true
    }

    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.22.1'
        }
    }
}

dependencies {
    implementation("com.google.android.flexbox:flexbox:3.0.0")
    implementation("net.lingala.zip4j:zip4j:2.11.5")
    implementation("androidx.appcompat:appcompat:1.7.1")
    implementation("com.google.android.material:material:1.12.0")
    implementation("androidx.constraintlayout:constraintlayout:2.2.1")
    implementation("com.google.android.material:material:1.12.0")
    implementation("androidx.preference:preference:1.2.1")
    implementation("org.apache.commons:commons-compress:1.27.1")
    implementation("com.google.code.gson:gson:2.13.1")
    implementation("com.github.vatbub:mslinks:1.0.6.2")
    implementation("com.squareup.okhttp3:okhttp:5.1.0")
}